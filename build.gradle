plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.12.0'
}

scmVersion {
    tag {
        initialVersion = { "1.0.0" }
    }
}

group 'com.theboreddev'
project.version = scmVersion.version

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.theboreddev'
            artifactId = 'javafixture'
            version = project.version
            from components.java

            pom {
                name = 'Java Fixture'
                description = 'A Java library to generate random test data'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/theboreddev/javafixture/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'theboreddev'
                        name = 'The Bored Dev'
                        email = 'account@theboreddev.com'
                    }
                }
                scm {
                    connection = 'git@github.com:theboreddev/javafixture.git'
                    developerConnection = 'git@github.com:theboreddev/javafixture.git'
                    url = 'https://github.com/theboreddev/javafixture.git'
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username = "$usr"
                password = "$pwd"
            }

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
}
